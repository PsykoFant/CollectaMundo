; Script generated by the Inno Setup Script Wizard.
#define MyAppName "CollectaMundo"
#define MyAppVersion "0.1.0"
#define MyAppExeName "CollectaMundo.exe"

[Setup]
AppId={{B627DCCF-A915-4BA0-B0BC-6EAC824AD045}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
DefaultDirName={userappdata}\{#MyAppName}
DisableDirPage=yes
DisableProgramGroupPage=yes
PrivilegesRequired=lowest
Compression=lzma
SolidCompression=yes
WizardStyle=modern
OutputBaseFilename=CollectaMundoInstaller
SetupIconFile=.\CollectaMundo\Resources\Images\Icon.ico
AlwaysRestart=no
UninstallDisplayIcon={app}\Icon.ico 

[CustomMessages]
ExistingInstallPrompt=It appears that {#MyAppName} is already installed. Do you want to upgrade to version {#MyAppVersion}?

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\Icon.ico"
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"; IconFilename: "{app}\Icon.ico"; 

[Run]
Filename: "{app}\{#MyAppExeName}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: nowait postinstall skipifsilent

[UninstallDelete]
Type: filesandordirs; Name: "{userappdata}\{#MyAppName}"

[Registry]
Root: HKCU; Subkey: "Software\{#MyAppName}"; ValueType: string; ValueName: "InstallLocation"; ValueData: "{app}"; Flags: uninsdeletevalue
Root: HKCU; Subkey: "Software\{#MyAppName}"; ValueType: string; ValueName: "Version"; ValueData: "{#MyAppVersion}"; Flags: uninsdeletevalue

[Code]
function SplitVersionString(const S: String; Delimiter: Char): TArrayOfString;
var
  I, StartIndex, SplitCount: Integer;
begin
  SplitCount := 0;
  SetArrayLength(Result, 0);
  StartIndex := 1;

  for I := 1 to Length(S) do
  begin
    if S[I] = Delimiter then
    begin
      SetArrayLength(Result, SplitCount + 1);
      Result[SplitCount] := Copy(S, StartIndex, I - StartIndex);
      StartIndex := I + 1;
      Inc(SplitCount);
    end;
  end;

  SetArrayLength(Result, SplitCount + 1);
  Result[SplitCount] := Copy(S, StartIndex, Length(S) - StartIndex + 1);
end;

function ParseVersionString(const Version: String; var Major, Minor, Patch: Integer): Boolean;
var
  Parts: TArrayOfString;
begin
  Result := False;
  Parts := SplitVersionString(Version, '.');
  if Length(Parts) = 3 then
  begin
    Major := StrToIntDef(Parts[0], -1);
    Minor := StrToIntDef(Parts[1], -1);
    Patch := StrToIntDef(Parts[2], -1);
    Result := (Major >= 0) and (Minor >= 0) and (Patch >= 0);
  end;
end;

function CompareVersions(const ExistingVersion, NewVersion: String): Integer;
var
  ExistingMajor, ExistingMinor, ExistingPatch: Integer;
  NewMajor, NewMinor, NewPatch: Integer;
begin
  if ParseVersionString(ExistingVersion, ExistingMajor, ExistingMinor, ExistingPatch) and
     ParseVersionString(NewVersion, NewMajor, NewMinor, NewPatch) then
  begin
    if ExistingMajor < NewMajor then
      Result := -1
    else if ExistingMajor > NewMajor then
      Result := 1
    else if ExistingMinor < NewMinor then
      Result := -1
    else if ExistingMinor > NewMinor then
      Result := 1
    else if ExistingPatch < NewPatch then
      Result := -1
    else if ExistingPatch > NewPatch then
      Result := 1
    else
      Result := 0;
  end
  else
    Result := 0;  // If parsing fails, assume versions are the same
end;

function InitializeSetup(): Boolean;
var
  ExistingInstallDir, ExistingVersion: String;
  VersionComparison: Integer;
begin
  Result := True;
  
  // Check if the application is already installed by querying the registry for 'InstallLocation'
  if RegQueryStringValue(HKCU, 'Software\{#MyAppName}', 'InstallLocation', ExistingInstallDir) and
     RegQueryStringValue(HKCU, 'Software\{#MyAppName}', 'Version', ExistingVersion) then
  begin
    VersionComparison := CompareVersions(ExistingVersion, '{#MyAppVersion}');
    
    case VersionComparison of
      -1: // Existing version is older than the new version
        begin
          if MsgBox(ExpandConstant('{cm:ExistingInstallPrompt}'), mbConfirmation, MB_YESNO) = IDNO then
          begin
            Result := False;  // Cancel the installation if the user selects No
          end;
        end;
      
      0: // Existing version is the same as the new version
        begin
          if MsgBox('The same version of {#MyAppName} is already installed. Would you like to reinstall it?', 
                    mbConfirmation, MB_YESNO) = IDNO then
          begin
            Result := False;  // Cancel the installation if the user selects No
          end;
        end;
      
      1: // Existing version is newer than the new version
        begin
          MsgBox('A newer version of {#MyAppName} is already installed. The setup will now exit.', mbInformation, MB_OK);
          Result := False;  // Exit the installation
        end;
    end;
  end;
end;


[Files]
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\CollectaMundo.deps.json"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\CollectaMundo.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\CollectaMundo.exe"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\CollectaMundo.exe.config"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\CollectaMundo.runtimeconfig.json"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\EntityFramework.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\EntityFramework.SqlServer.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\libSkiaSharp.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\Newtonsoft.Json.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\ServiceStack.Client.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\ServiceStack.Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\ServiceStack.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\ServiceStack.Interfaces.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\ServiceStack.Text.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Converters.Wpf.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Css.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Dom.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Model.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Rendering.Gdi.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Rendering.Wpf.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SharpVectors.Runtime.Wpf.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SkiaSharp.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SkiaSharp.Extended.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SkiaSharp.Extended.Svg.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SkiaSharp.Views.Desktop.Common.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SkiaSharp.Views.WPF.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\sni.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\SQLite.Interop.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\System.Data.SqlClient.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\System.Data.SQLite.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\System.Data.SQLite.EF6.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\bin\Release\net8.0-windows\win-x64\System.Linq.Dynamic.Core.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: ".\CollectaMundo\Resources\Images\Icon.ico"; DestDir: "{app}"; Flags: ignoreversion